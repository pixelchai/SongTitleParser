%ignore /\s/
// common
_NAME_LETTER: /[ \w!?。\.]/
NAME: _NAME_LETTER+
NAME_COMMAS: (_NAME_LETTER | _COMMA)+
_LBRAC: "(" | "[" | "『" | "「" | "（"
_RBRAC: ")" | "]" | "』" | "」" | "）"
_QUOTE: "\""
_COMMA: ","
_DASH: "-"+
_PLUS: "+"
_SLASH: "/" | "／"
_NAME_SEP: _PLUS | _SLASH
_SEP: _DASH
_REV_SEP: _SLASH
_SEP_COMMA: _SEP  | _COMMA

DATE: /\d{4}/ // year

?start: standard
        | relaxed

relaxed: _QUOTE song_title_unam _QUOTE (meta_official | meta_mv)? "by" artist_name

standard: artist_name _SEP song_title_unam (_LBRAC _meta_unam _RBRAC)+
          | artist_name _SEP song_title (_LBRAC meta _RBRAC)*
          | (_LBRAC meta _RBRAC)+ artist_name _SEP song_title
          | artist_name _SEP song_title _SEP_COMMA meta
          | artist_name _LBRAC song_title _RBRAC (_SEP_COMMA? meta)
          | song_title _REV_SEP artist_name

_feat: ("ft" | "featuring" | "feat")
artist_name: _artist_name_core (_feat)? (_NAME_SEP _artist_name_core)+
             | _artist_name_core

_artist_name_core: NAME
                   | _name_and_translated

song_title_unam: NAME
song_title: NAME
            | _name_and_translated

// translation in brackets
_name_and_translated: _LBRAC NAME_COMMAS _RBRAC NAME
                      | _QUOTE NAME _QUOTE _LBRAC NAME_COMMAS _RBRAC
                      | NAME _LBRAC NAME_COMMAS _RBRAC
                      | NAME _QUOTE NAME_COMMAS _QUOTE

// todo: make these all _meta_whatever later
?meta: _meta_unam
       | meta_lyrics
       | meta_live

// (reasonably) unambigious meta
_meta_unam: meta_mv
            | meta_official
            | meta_youtube

meta_mv: "music video" | "MV" | "AMV" | "Animation"
meta_official: "official" (meta_mv | "video" | "audio")
!meta_youtube: "HQ" | "HD"
meta_live: ("live" ("in"| "at"))? _live_info
           | _live_info _SEP_COMMA? "live"
           | "live"
_live_info: NAME (_SEP_COMMA NAME)? (_SEP_COMMA DATE)?
            | NAME (_SEP_COMMA DATE)? (_SEP_COMMA NAME)?

meta_lyrics: _lyrics_info (_NAME_SEP? _lyrics_keyword)?
             | "easy"? _lyrics_keyword
_lyrics_keyword: ("lyric" | "subtitle" | "sub") "s"?
_lyrics_info: NAME (_NAME_SEP NAME)

_utattemita: "歌ってみた" | "tried to sing" | "tried singing"