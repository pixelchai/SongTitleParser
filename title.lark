%ignore /\s/
// common
_NAME_LETTER: /[\s!?。・]|[^\W_]/  // match any character in the first class OR (\w but not _ [special underscore])
NAME: (_NAME_LETTER | "." | "'")+
_NAME_LETTER_LATIN: /[\s!?A-Za-z0-9]/
NAME_LATIN: (_NAME_LETTER_LATIN | ".")+
NAME_LATIN_COMMAS: (_NAME_LETTER_LATIN | "." | _COMMA | "'")+  // NB: also allow apostrophes here (hacky)
NAME_NO_DOT: _NAME_LETTER+
NAME_COMMAS: (_NAME_LETTER | "." | _COMMA)+
_LBRAC: "(" | "[" | "『" | "「" | "（" | "【" | "［"
_RBRAC: ")" | "]" | "』" | "」" | "）" | "】" | "］"
_QUOTE: "\"" | "'"
_COMMA: "," | "、"
_DASH: ("-" | "_" | "_" | "—" )+
_PLUS: "+"
_SLASH: "/" | "／"
_NAME_SEP: _PLUS | _SLASH | "&"
_SEP: _DASH
_REV_SEP: _SLASH
_SEP_COMMA: _SEP  | _COMMA

DATE: /\d{4}/ // year

?start: relaxed
        | special_case_prefix? standard

relaxed: _QUOTE song_title_unam _QUOTE (meta_official | meta_mv)? "by" artist_name

standard: artist_name _SEP song_title_pre_meta_unam _meta_unam (_LBRAC _meta_unam _RBRAC)* // simple special case
          | artist_name _SEP song_title_unam (_LBRAC _meta_unam _RBRAC)+
          | (_LBRAC _meta_unam _RBRAC)+ artist_name _SEP song_title
          | song_title (_SEP | _REV_SEP) artist_name_ft (_LBRAC meta _RBRAC)*
          | song_title artist_ft ((_LBRAC meta _RBRAC)* | _SEP_COMMA meta)?
          | artist_name _SEP song_title (_LBRAC meta _RBRAC)*
          | (_LBRAC meta _RBRAC)+ artist_name _SEP song_title
          | song_title _REV_SEP artist_name (_LBRAC meta _RBRAC)+
          | artist_name _SEP song_title _SEP_COMMA meta
          | song_title ((_LBRAC meta _RBRAC)* | meta)
          | artist_name _LBRAC song_title _RBRAC (_SEP_COMMA? meta)
          | (_LBRAC meta _RBRAC)* song_title (_LBRAC artist_name _RBRAC)
          | (_LBRAC meta _RBRAC)* song_title _REV_SEP artist_name
          | song_title

special_case_prefix: "a super nice japanese song" _SEP

_feat: ("ft" | "feat" | "featuring" ) ("." | ":")?
_artist_ft: _feat NAME_NO_DOT (_NAME_SEP NAME)*
artist_ft: _artist_ft | _LBRAC _artist_ft _RBRAC
artist_name_ft: /[\s\w!?]+(?=(ft|featuring|feat)[.:]?)/ _artist_ft
artist_name: artist_name_ft
             | _artist_name_core

_artist_name_core: NAME
                   | _name_and_translated
artist_name_core: _artist_name_core

_song_title_unam: NAME | _QUOTE NAME_COMMAS _QUOTE
song_title_unam: _song_title_unam
_song_title_unam_latin: NAME_LATIN | _QUOTE NAME_COMMAS _QUOTE
song_title_unam_latin: _song_title_unam_latin

song_title_pre_meta_unam: /([\s!?。・]|[^\W_])+(?=music video|mv|amv|animation|official|HQ|HD|cover)/

song_title: _name_and_translated
            | _song_title_unam

// translation in brackets
_name_and_translated: _LBRAC NAME_COMMAS _RBRAC NAME_COMMAS
                      | song_title_unam_latin _LBRAC NAME_COMMAS _RBRAC
                      | song_title_unam _LBRAC NAME_LATIN_COMMAS _RBRAC
                      | NAME_LATIN _LBRAC NAME_COMMAS _RBRAC
                      | NAME _LBRAC NAME_LATIN_COMMAS _RBRAC
                      | NAME_LATIN _QUOTE NAME_COMMAS _QUOTE
                      | NAME _QUOTE NAME_LATIN_COMMAS _QUOTE
                      | NAME_COMMAS _LBRAC NAME_LATIN_COMMAS _RBRAC
                      | NAME_LATIN _LBRAC NAME_COMMAS _RBRAC

// todo: make these all _meta_whatever later
?meta: _meta_unam
       | meta_lyrics
       | meta_live
       | meta_unknown

meta_unknown: NAME_COMMAS (_SEP NAME_COMMAS)*

// (reasonably) unambigious meta
_meta_unam: meta_mv
            | meta_cover
            | meta_prod
            | meta_official
            | meta_youtube

meta_prod: _meta_prod_relaxed
_meta_prod_relaxed: "produced by " _artist_name_core

meta_cover: "cover" | _utattemita
meta_mv: "music video" | "MV" | "AMV" | "Animation"
meta_official: "official" (meta_mv | "video" | "audio")
!meta_youtube: "HQ" | "HD"

meta_live: ("live" ("in"| "at"))? _live_info
           | _live_info _SEP_COMMA? "live"
           | "live"
_live_info: NAME (_SEP_COMMA NAME)? (_SEP_COMMA DATE)?
            | NAME (_SEP_COMMA DATE)? (_SEP_COMMA NAME)?

meta_lyrics: _lyrics_info (_NAME_SEP? _lyrics_keyword)?
             | _lyrics_keyword NAME   // stuff like 'lyrics Español'
             | "easy"? _lyrics_keyword
_lyrics_keyword: ("lyric" | "subtitle" | "sub") "s"?
_lyrics_info: NAME (_NAME_SEP NAME)

_utattemita: "歌ってみた" | "tried to sing" | "tried singing" | "sang it"